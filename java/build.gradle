import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "base"
    id "signing"
    id "com.diffplug.spotless" version "6.20.0"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "org.jetbrains.kotlin.jvm" version "2.1.0"
}

allprojects {
    version = "0.73.2"
    group   = "org.signal"

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions.jvmTarget = JvmTarget.JVM_17
    }
}

subprojects {
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.encoding = 'UTF-8'
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    allprojects {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs += ["-Xlint:deprecation", "-Xlint:fallthrough", "-Xlint:unchecked"]
        }
        tasks.withType(AbstractArchiveTask) {
            preserveFileTimestamps = false
            reproducibleFileOrder = true
        }
    }

    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target('**/*.kt')
            targetExclude("**/acknowledgments/**")
            ktlint()
        }
        java {
            target('**/*.java')
            targetExclude('**/Native.java', '**/NativeTesting.java')
            importOrder()
            removeUnusedImports()

            googleJavaFormat()
            formatAnnotations()
            licenseHeaderFile rootProject.file('license_header.txt')
        }
    }
}

task makeJniLibrariesDesktop(type:Exec) {
  group 'Rust'
  description 'Build the JNI libraries'

  def debugLevelLogsFlag = project.hasProperty('debugLevelLogs') ? ['--debug-level-logs'] : []
  def jniTypeTaggingFlag = project.hasProperty('jniTypeTagging') ? ['--jni-type-tagging'] : []
  def debugFlag = project.hasProperty('debugRust') ? ['--debug'] : []
  // Explicitly specify 'bash' for Windows compatibility.
  commandLine 'bash', './build_jni.sh', *debugLevelLogsFlag, *jniTypeTaggingFlag, *debugFlag, 'desktop'
}

task makeJniLibrariesServer(type:Exec) {
  group 'Rust'
  description 'Build the JNI libraries'

  def debugLevelLogsFlag = project.hasProperty('debugLevelLogs') ? ['--debug-level-logs'] : []
  def jniTypeTaggingFlag = project.hasProperty('jniTypeTagging') ? ['--jni-type-tagging'] : []
  def debugFlag = project.hasProperty('debugRust') ? ['--debug'] : []
  def target = project.hasProperty('crossCompileServer') ? 'server-all' : 'server'
  // Explicitly specify 'bash' for Windows compatibility.
  commandLine 'bash', './build_jni.sh', *debugLevelLogsFlag, *jniTypeTaggingFlag, *debugFlag, target
}

task cargoClean(type:Exec) {
  group 'Rust'
  commandLine 'cargo', 'clean'
}

task cleanJni(type: Delete) {
  description 'Clean JNI libs'
  delete fileTree('./android/src/main/jniLibs') {
    include '**/*.so'
  }
  delete fileTree('./client/src/main/resources') {
    include '**/*.so'
    include '**/*.dylib'
    include '**/*.dll'
  }
  delete fileTree('./server/src/main/resources') {
    include '**/*.so'
    include '**/*.dylib'
    include '**/*.dll'
  }
}

clean.dependsOn([cargoClean, cleanJni])

// PUBLISHING

ext.setUpSigningKey = { signingExt ->
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    if (signingKeyId && signingKey && signingPassword) {
        signingExt.useInMemoryPgpKeys(signingKeyId.trim(), signingKey.trim(), signingPassword.trim())
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.findProperty('sonatypeUsername') ?: ""
            password = project.findProperty('sonatypePassword') ?: ""
        }
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}
